resource "aws_instance" "f" {
  ami                         = data.aws_ami.amazon-linux-2.id
  subnet_id                   = module.vpc.public_subnets[0]
  instance_type               = local.ec2_instance_type
  associate_public_ip_address = true
  key_name                    = aws_key_pair.v.key_name
  tags                        = local.common_tags
  volume_tags                 = local.common_tags
  #iam_instance_profile        = aws_iam_instance_profile.f.name
}

resource "aws_route53_record" "ssh" {
  zone_id = data.aws_route53_zone.public.id
  name    = "${local.project}.${local.dns_hosted_zone_name}"
  type    = "A"
  ttl     = "300"
  records = [aws_instance.f.public_ip]
}

resource "aws_elb" "f" {
  name    = "${local.project}-elb"
  subnets = module.vpc.public_subnets

  listener {
    instance_port      = 80
    instance_protocol  = "http"
    lb_port            = 443
    lb_protocol        = "https"
    ssl_certificate_id = aws_acm_certificate.f.arn
  }

  health_check {
    healthy_threshold   = 2
    unhealthy_threshold = 2
    timeout             = 3
    target              = "HTTP:80/"
    interval            = 30
  }

  instances                   = [aws_instance.f.id]
  cross_zone_load_balancing   = true
  idle_timeout                = 400
  connection_draining         = true
  connection_draining_timeout = 400
}

resource "aws_route53_record" "w" {
  zone_id = data.aws_route53_zone.public.id
  name    = "www.${local.dns_hosted_zone_name}"
  type    = "CNAME"
  ttl     = "300"
  records = [aws_elb.f.dns_name]
}

output "aws_instance_id" {
  value = aws_instance.f.id
}
output "finance_url" {
  value = "https://f.${local.dns_hosted_zone_name}"
}
